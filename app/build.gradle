apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def storePwd = properties.getProperty("keystore.storePassword")
def keyPwd = properties.getProperty("keystore.keyPassword")
def alias = properties.getProperty("keystore.keyAlias")

def BOOLEAN = 'boolean'
def STRING = 'String'
def INT = 'int'
def TRUE = 'true'
def FALSE = 'false'

def GIT_SHA = 'GIT_SHA'
def BUILD_TIME = 'BUILD_TIME'

def CONNECTION_TIMEOUT = 'CONNECTION_TIMEOUT'
def REST_ENDPOINT = 'REST_ENDPOINT'


android {
    publishNonDefault true

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion "${rootProject.ext.buildToolsVersion}"

    defaultConfig {
        applicationId "com.molmc.cypress"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField STRING, GIT_SHA, "\"${rootProject.ext.gitSha}\""
        buildConfigField STRING, BUILD_TIME, "\"${rootProject.ext.buildTime}\""
        buildConfigField INT, CONNECTION_TIMEOUT, '2000'
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
        }

        release {
            keyAlias alias
            keyPassword keyPwd
            storeFile file('../release.keystore')
            storePassword storePwd
        }
    }

    productFlavors {
        dev {
            applicationId "${project.android.defaultConfig.applicationId}.dev"
            versionName "${project.android.defaultConfig.versionName}-beta-${rootProject.ext.gitSha}"
            buildConfigField STRING, REST_ENDPOINT, '""'
        }
        prod {
            applicationId project.android.defaultConfig.applicationId
            buildConfigField STRING, REST_ENDPOINT, '""'
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    //supportLib
    compile rootProject.ext.supportLibAppCompat
    compile rootProject.ext.supportLibDesign
    compile rootProject.ext.supportLibRecyclerview
    compile rootProject.ext.supportLibCardview

    // RxAndroid
    // https://github.com/ReactiveX/RxAndroid
    // https://github.com/ReactiveX/RxJava
    // https://github.com/ReactiveX/RxJavaJoins
    compile 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.6'

    // 注解框架
    final BUTTERKNIFE_VERSION = '8.2.1'
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    // Dagger2
    // https://github.com/google/dagger
    final DAGGER_VERSION = '2.5'
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // eventbus
    // https://github.com/greenrobot/EventBus
    compile 'org.greenrobot:eventbus:3.0.0'

    // spots-dialog
    // https://github.com/d-max/spots-dialog
    compile 'com.github.d-max:spots-dialog:0.7@aar'

    // AppIntro
    // https://github.com/PaoloRotolo/AppIntro
    compile 'com.github.paolorotolo:appintro:4.0.0'

    // https://github.com/roughike/BottomBar
    compile 'com.roughike:bottom-bar:2.0.2'

    // BaseRecyclerViewAdapterHelper
    // https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:v1.9.7'

    // recycler view divider
    // https://github.com/yqritc/RecyclerView-FlexibleDivider
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'

    //数据统计
    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }

    //内存泄露检查
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    compile project(':core')
    compile project(':mvp')
}
